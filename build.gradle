description = 'JCL'
group = 'jcl'
version = '1.0-SNAPSHOT'

apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
}

compileJava {
	options.compilerArgs << "-Xlint"
}

String COMMONS_LANG_VERSION = '3.4';
String COMMONS_MATH_VERSION = '3.5';
String COMMONS_COLLECTIONS_VERSION = '4.0';
String COMMONS_IO_VERSION = '2.4';

String SPRING_VERSION = '4.1.6.RELEASE';

String ASM_ALL_VERSION = '5.0.3';

String JAVA_HELP = '2.0.05';

String SLF4J_VERSION = '1.7.12';
String LOGBACK_VERSION = '1.1.3';

String TESTNG_VERSION = '6.8.21';

dependencies {
	compile group: 'org.apache.commons', name: 'commons-lang3', version: "$COMMONS_LANG_VERSION"
	compile group: 'org.apache.commons', name: 'commons-math3', version: "$COMMONS_MATH_VERSION"
	compile group: 'org.apache.commons', name: 'commons-collections4', version: "$COMMONS_COLLECTIONS_VERSION"
	compile group: 'commons-io', name: 'commons-io', version: "$COMMONS_IO_VERSION"

	compile group: 'org.ow2.asm', name: 'asm-all', version: "$ASM_ALL_VERSION"

	compile group: 'org.springframework', name: 'spring-context', version: "$SPRING_VERSION"
	compile group: 'org.springframework', name: 'spring-context-support', version: "$SPRING_VERSION"

	compile group: 'javax.help', name: 'javahelp', version: "$JAVA_HELP"

	compile group: 'org.slf4j', name: 'slf4j-api', version: "$SLF4J_VERSION"
	compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: "$SLF4J_VERSION"
	compile group: 'org.slf4j', name: 'jul-to-slf4j', version: "$SLF4J_VERSION"
	compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: "$SLF4J_VERSION"
	compile group: 'org.slf4j', name: 'osgi-over-slf4j', version: "$SLF4J_VERSION"

	compile group: 'ch.qos.logback', name: 'logback-core', version: "$LOGBACK_VERSION"
	compile group: 'ch.qos.logback', name: 'logback-classic', version: "$LOGBACK_VERSION"

	testCompile group: 'org.testng', name: 'testng', version: "$TESTNG_VERSION"
}

apply plugin: 'application'
mainClassName = "jcl.system.JCL"

task copyToLib(type: Copy) {
	into "$buildDir/libs"
	from configurations.runtime
}
build.dependsOn(copyToLib)

apply plugin: 'jacoco'
jacoco {
	toolVersion = '0.7.4.201502262128'
}
test {
	useTestNG()

	jacoco {
		append = true
		destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
		classDumpFile = file("$buildDir/jacoco/classpathdumps")
	}
}
jacocoTestReport {
	group = "Reporting"
	description = "Generate Jacoco coverage reports after running tests."
	additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
	reports {
		xml.enabled false
		csv.enabled false
		html.destination "$buildDir/jacocoHtml"
	}
}

apply plugin: 'sonar-runner'
sonarRunner {
	sonarProperties {
		property "sonar.host.url", "http://localhost:9000"
		property "sonar.jdbc.url", "jdbc:h2:tcp://localhost:9092/sonar"
		property "sonar.jdbc.username", "sonar"
		property "sonar.jdbc.password", "sonar"
	}
}
